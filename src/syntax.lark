start: header_statement* body_statement*

header_statement: title_size
                | title
                | participant_width
                | participant_spacing
                | participant

body_statement: activation
              | deactivation
              | alternative
              | option
              | loop
              | vertical_offset
              | frame_dimension
              | note
              | message
              | self_call

title: "title" _SPACE+ TEXT _NEWLINE
title_size: "title size" _SPACE+ NUMBER _SPACE+ NUMBER _NEWLINE

participant: "participant" _SPACE+ name participant_alias? _NEWLINE
participant_alias: _SPACE+ "as" _SPACE+ name

participant_width: "participant width" _SPACE+ NUMBER _NEWLINE
participant_spacing: "participant spacing" _SPACE+ NUMBER _NEWLINE

activation: "activate" _SPACE+ name _NEWLINE
deactivation: "deactivate" _SPACE+ name _NEWLINE

alternative: "alt" _SPACE+ TEXT _NEWLINE inner_statements alternative_branches "end" _NEWLINE
alternative_branches: alternative_branch*
alternative_branch: "else" (_SPACE+ TEXT)? _NEWLINE inner_statements

option: "opt" _SPACE+ TEXT _NEWLINE inner_statements "end" _NEWLINE
loop: "loop" _SPACE+ TEXT _NEWLINE inner_statements "end" _NEWLINE

inner_statements: body_statement+

note: "note on" _SPACE+ name note_attrs _NEWLINE note_text "end" _NEWLINE
note_text: /.+?(?=^[ \t]*end[ \t]*$)/sm
note_attrs: (note_dx | note_dy | note_width | note_height)*
note_dx: _SPACE+ "dx" _SPACE+ NUMBER
note_dy: _SPACE+ "dy" _SPACE+ NUMBER
note_width: _SPACE+ "width" _SPACE+ NUMBER
note_height: _SPACE+ "height" _SPACE+ NUMBER

message: name _SPACE+ arrow _SPACE+ name (_SPACE* ":" _SPACE+ TEXT)? _NEWLINE
self_call: name _SPACE* ":" _SPACE+ TEXT _NEWLINE

vertical_offset: "vertical offset" _SPACE+ NUMBER _NEWLINE
frame_dimension: "frame for" _SPACE+ name _SPACE+ "dx" _SPACE+ NUMBER _NEWLINE

arrow: ARROW_LINE ARROW_END ARROW_ACTIVATION?
ARROW_LINE: /-{1,2}/
ARROW_END: />{1,2}/
ARROW_ACTIVATION: /\+|-|\|/

name: QUOTED_NAME | UNQUOTED_NAME
QUOTED_NAME: /"[^\n]*"/
UNQUOTED_NAME: /[A-Za-z0-9_]+/

TEXT: /[^\n]+/
NUMBER: /-?[0-9]+/

_SPACE: /[ \t]+/
_NEWLINE: "\n"
_BEGIN: /^/m
_END: /$/m

%ignore _BEGIN _SPACE* /\/\/.*/ _NEWLINE // comments
%ignore _BEGIN _SPACE* _NEWLINE // empty lines
%ignore _BEGIN _SPACE+ // leading white space
%ignore _SPACE+ _END // trailing white space
%ignore "\r" // windows line endings
