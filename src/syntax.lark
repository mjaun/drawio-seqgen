start: statement_list

statement_list: statement*

statement: title_width
         | title_height
         | title
         | participant_width
         | participant_spacing
         | participant
         | activation
         | deactivation
         | alternative
         | option
         | loop
         | group
         | vertical_offset
         | frame_dimension
         | note
         | found_message
         | lost_message
         | message
         | self_call

title: "title" _SPACE+ TEXT _NEWLINE
title_width: "title width" _SPACE+ NUMBER _NEWLINE
title_height: "title height" _SPACE+ NUMBER _NEWLINE

participant: "participant" _SPACE+ name (_SPACE+ "as" _SPACE+ name)? _NEWLINE
participant_width: "participant width" _SPACE+ NUMBER _NEWLINE
participant_spacing: "participant spacing" _SPACE+ NUMBER _NEWLINE

activation: "activate" _SPACE+ name (_SPACE+ name)* _NEWLINE
deactivation: "deactivate" _SPACE+ name (_SPACE+ name)* _NEWLINE

alternative: "alt" _SPACE+ TEXT _NEWLINE statement_list alternative_branch* "end" _NEWLINE
alternative_branch: "else" (_SPACE+ TEXT)? _NEWLINE statement_list

option: "opt" _SPACE+ TEXT _NEWLINE statement_list "end" _NEWLINE
loop: "loop" _SPACE+ TEXT _NEWLINE statement_list "end" _NEWLINE
group: "group" _SPACE+ TEXT _NEWLINE statement_list "end" _NEWLINE

note: "note on" _SPACE+ name (note_dx | note_dy | note_width | note_height)* _NEWLINE note_text "end note" _NEWLINE
note_text: /.+?(?=^[ \t]*end note[ \t]*$)/sm
note_dx: _SPACE+ "dx" _SPACE+ NUMBER
note_dy: _SPACE+ "dy" _SPACE+ NUMBER
note_width: _SPACE+ "width" _SPACE+ NUMBER
note_height: _SPACE+ "height" _SPACE+ NUMBER

found_message: "found" _SPACE+ /left|right/ (_SPACE+ NUMBER)? _SPACE+ arrow _SPACE+ name (_SPACE* ":" _SPACE+ TEXT)? _NEWLINE
lost_message: name _SPACE+ arrow _SPACE+ "lost" _SPACE+ /left|right/ (_SPACE+ NUMBER)? (_SPACE* ":" _SPACE+ TEXT)? _NEWLINE
message: name _SPACE+ arrow _SPACE+ name (_SPACE* ":" _SPACE+ TEXT)? _NEWLINE
self_call: name _SPACE* ":" _SPACE+ TEXT _NEWLINE

vertical_offset: "vertical offset" _SPACE+ NUMBER _NEWLINE
frame_dimension: "frame extend" _SPACE+ NUMBER _NEWLINE

arrow: ARROW_LINE ARROW_END ARROW_ACTIVATION?
ARROW_LINE: /-{1,2}/
ARROW_END: />{1,2}/
ARROW_ACTIVATION: /\+|-|\|/

name: QUOTED_NAME | UNQUOTED_NAME
QUOTED_NAME: /"[^\n]*"/
UNQUOTED_NAME: /[A-Za-z0-9_]+/

TEXT: /[^\n]+/
NUMBER: /-?[0-9]+/

_SPACE: /[ \t]+/
_NEWLINE: "\n"
_BEGIN: /^/m
_END: /$/m

%ignore _BEGIN _SPACE* /\/\/.*/ _NEWLINE // comments
%ignore _BEGIN _SPACE* _NEWLINE // empty lines
%ignore _BEGIN _SPACE+ // leading white space
%ignore _SPACE+ _END // trailing white space
