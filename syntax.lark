start: declaration_list statement_list

declaration_list: declaration+
statement_list: statement+

declaration: title_size
           | title
           | participant_width
           | participant_spacing
           | participant

statement: activation
         | deactivation
         | alternative
         | option
         | loop
         | vertical_offset
         | frame_dimension
         | message
         | self_call

title: "title" _SPACE+ TEXT _NEWLINE
title_size: "title size" _SPACE+ NUMBER "x" NUMBER _NEWLINE

participant: "participant" _SPACE+ name participant_alias? _NEWLINE
participant_alias: _SPACE+ "as" _SPACE+ name

participant_width: "participant width" _SPACE+ NUMBER _NEWLINE
participant_spacing: "participant spacing" _SPACE+ NUMBER _NEWLINE

vertical_offset: "vertical offset" _SPACE+ NUMBER _NEWLINE
frame_dimension: "frame dimension for" _SPACE+ name _SPACE+ "dx" _SPACE+ NUMBER _NEWLINE

activation: "activate" _SPACE+ name _NEWLINE
deactivation: "deactivate" _SPACE+ name _NEWLINE

message: name _SPACE+ arrow _SPACE+ name _SPACE* ":" _SPACE* TEXT _NEWLINE
self_call: name _SPACE* ":" _SPACE* TEXT _NEWLINE

alternative: "alt" _SPACE+ TEXT _NEWLINE statement_list alternative_else+ "end" _NEWLINE
alternative_else: "else" _SPACE+ TEXT _NEWLINE statement_list
option: "opt" _SPACE+ TEXT _NEWLINE statement_list "end" _NEWLINE
loop: "loop" _SPACE+ TEXT _NEWLINE statement_list "end" _NEWLINE

arrow: ARROW_LINE ARROW_END ARROW_ACTIVATION?
ARROW_LINE: /-{1,2}/
ARROW_END: />{1,2}/
ARROW_ACTIVATION: /\+|-|\|/

name: QUOTED_NAME | UNQUOTED_NAME
QUOTED_NAME: /"[^\n]*"/
UNQUOTED_NAME: /[A-Za-z0-9_]+/

TEXT: /[^\n]+/
NUMBER: /-?[0-9]+/

_BEGIN: /(?m)^/
_END: /(?m)$/
_SPACE: /[ \t]+/
_NEWLINE: /\r?\n/

%ignore _BEGIN _SPACE* _NEWLINE // empty lines
%ignore _BEGIN _SPACE+ // leading white space
%ignore _SPACE+ _END // trailing white space
%ignore _SPACE* /\/\/.*/ _NEWLINE // comments
